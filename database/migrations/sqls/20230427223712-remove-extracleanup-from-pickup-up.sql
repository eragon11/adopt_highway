--
-- Remove the PICKUP.EXTRA_CLEANUP
--
IF EXISTS(SELECT *
FROM sys.columns c INNER JOIN sys.objects o on o.OBJECT_ID = c.OBJECT_ID
WHERE o.type = 'U' and o.name = 'PICKUP' AND c.name = 'EXTRA_CLEANUP')
BEGIN
    ALTER TABLE [PICKUP] DROP COLUMN EXTRA_CLEANUP;
END

-- update the legacy proc
EXEC sp_EXECUTESQL N'
ALTER
      PROCEDURE [aah].[aah_insert_missing_pickups]
AS
BEGIN;
SET NOCOUNT ON;
    INSERT INTO [PICKUP_SCHEDULE]
        (
        [AGREEMENT_ID],
        [TYPE],
        [SCHEDULED_PICKUP_YEAR_MONTH],
        [SCHEDULED_PICKUP_DATE],
        [ESTIMATED_BAG_QUANTITY],
        [ESTIMATED_VEST_QUANTITY]
        )
    SELECT t1.[AAH_CNTRCT_ID],
        t1.[PICKUP_TYPE_NM],
        t1.[SCHED_PICKUP_ESTMTD_MTH_YR_DT],
        t1.[SCHED_PICKUP_ACTL_SCHED_DT],
        t1.[SCHED_PICKUP_ESTMTD_BAG_QTY],
        t1.[SCHED_PICKUP_ESTMTD_VEST_QTY]
    FROM aah_legacy..[SCHED_PICKUP] t1
        LEFT JOIN [PICKUP_SCHEDULE] t2 on t1.AAH_CNTRCT_ID = t2.AGREEMENT_ID
            AND t1.PICKUP_TYPE_NM = t2.TYPE
            AND t1.SCHED_PICKUP_ESTMTD_MTH_YR_DT = t2.SCHEDULED_PICKUP_YEAR_MONTH
    WHERE t2.AGREEMENT_ID IS NULL;
    INSERT INTO [PICKUP]
        (
        [AGREEMENT_ID],
        [TYPE],
        [ACTUAL_PICKUP_DATE],
        [BAG_FILL_QUANTITY],
        [VOLUNTEER_COUNT],
        [UNUSUAL_ITEM_DESCRIPTION],
        [COMMENT]
        )
    SELECT [AAH_CNTRCT_ID],
        [PICKUP_TYPE_NM],
        [AAH_PICKUP_DT],
        [PICKUP_BAG_FILL_QTY],
        [PICKUP_VOLR_QTY],
        [PICKUP_UNUSL_ITEM_DSCR],
        [PICKUP_CMNT]
    FROM aah_legacy..[PICKUP] t1
        LEFT JOIN PICKUP t2 ON t1.[AAH_CNTRCT_ID] = t2.[AGREEMENT_ID]
            AND [PICKUP_TYPE_NM] = t2.[TYPE]
            AND [AAH_PICKUP_DT] = t2.[ACTUAL_PICKUP_DATE]
            AND [PICKUP_BAG_FILL_QTY] = t2.[BAG_FILL_QUANTITY]
            AND [PICKUP_VOLR_QTY] = t2.[VOLUNTEER_COUNT]
            AND [PICKUP_UNUSL_ITEM_DSCR] = t2.[UNUSUAL_ITEM_DESCRIPTION]
            AND [PICKUP_CMNT] = t2.COMMENT
    WHERE t2.AGREEMENT_ID IS NULL;
SET NOCOUNT OFF;    
END;
';